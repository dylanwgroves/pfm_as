
/* Basics ______________________________________________________________________

	Project: Wellspring Tanzania, Spillovers
	Purpose: Table Construction - Attitude Results, HIV
	Author: dylan groves, dylanwgroves@gmail.com
	Date: 2020/12/23
________________________________________________________________________________*/


/* Introduction ________________________________________________________________*/
	
	clear all	
	clear matrix
	clear mata
	set more off
	global c_date = c(current_date)
	tempfile dta_main 
	tempfile dta_vill

/* Run Prelim File _____________________________________________________________	

	do "${code}/pfm_.master/00_setup/pfm_paths_master.do"
	do "${code}/pfm_ne/pfm_ne_prelim.do"
*/

/* Load Data ___________________________________________________________________*/	

	import excel "${spill_tables}/pfm_spill_analysis_hiv_main.xlsx", sheet("hiv_priority") firstrow clear
	cap rename A variable
	save `dta_main', replace

	foreach survey in partner friend kid {
		import excel "${spill_tables}/pfm_spill_analysis_hiv_`survey'.xlsx", sheet("hiv_priority") firstrow clear 
		cap rename A variable
		append using `dta_main', force
		save `dta_main', replace
	}
	
/* Sort Data ___________________________________________________________________*/

	/* Individual Data */
	sort pval
	destring 	variable variablelabel coef se pval r2 N ///
				lasso_coef lasso_se lasso_pval ripval lasso_ripval lasso_r2 lasso_N ///
				lasso_ctls lasso_ctls_num ///
				treat_mean treat_sd ctl_mean ctl_sd vill_sd min max, replace
	save `dta_main', replace

sort variable


/* Create Rank _________________________________________________________________*/

	/* Individual Data */
	# d ;
	local sumstat_vars 	
						p_hiv_elect
						p_ptixpref_hiv
						p_ptixpref_hiv_first
						p_ptixpref_hiv_topthree
						p_ptixpref_hiv_notlast
						;
	#d cr 
						
						

	gen rank = .
	local i = 1
	
	foreach var of local sumstat_vars {
		replace rank = `i' if variable == "`var'"
		local i = `i' + 1
	}		   

	drop if rank == .
	gsort rank


/* Any Labelling Changes _______________________________________________________*/

	tostring variablelabel, replace
	replace variablelabel = "HIV Vote" if variable == "hiv_elect"
	replace variablelabel = "All (1-8)" if variable == "ptixpref_hiv"
	replace variablelabel = "1st" if variable == "ptixpref_hiv_first"
	replace variablelabel = "Top 3" if variable == "ptixpref_hiv_topthree"
	replace variablelabel = "Not last" if variable == "ptixpref_hiv_notlast"
	
	replace variablelabel = "HIV Vote" if variable == "p_hiv_elect"
	replace variablelabel = "All (1-8)" if variable == "p_ptixpref_hiv"
	replace variablelabel = "1st" if variable == "p_ptixpref_hiv_first"
	replace variablelabel = "Top 3" if variable == "p_ptixpref_hiv_topthree"
	replace variablelabel = "Not last" if variable == "p_ptixpref_hiv_notlast"


/* Create variables ____________________________________________________________*/

	count
	global count = `r(N)'
	
	forval i = 1/$count {
	
		/* Drop pre-existing macros */
		macro drop var`i' b`i' se`i' p`i' r2`i' n`i' ctl`i' c`i' sd`i' mn`i' mx`i'

		global var`i' = variablelabel[`i']
		
		/* Normal */
		global b`i' = strofreal(coef[`i'], "%6.3f")	
		global se`i' = strofreal(se[`i'], "%6.3f")
		global p`i' = strofreal(ripval[`i'], "%6.3f")
		global r`i' = strofreal(r2[`i'], "%6.2f")
		global n`i' = strofreal(N[`i'], "%6.0fc")
		global ctl`i' = "No"
		
		/* Lasso */
		global b`i'c = strofreal(lasso_coef[`i'], "%6.3f")
		global se`i'c = strofreal(lasso_se[`i'], "%6.3f")
		global p`i'c = strofreal(lasso_ripval[`i'], "%6.3f")
		global r`i'c = strofreal(lasso_r2[`i'], "%6.2f")
		global n`i'c = strofreal(lasso_N[`i'], "%6.0fc")
		global ctl`i'c = strofreal(lasso_ctls_num[`i'], "%6.0fc")
		
		/* Summary stats */
		global c`i' = strofreal(ctl_mean[`i'], "%6.2f")
		global sd`i' = strofreal(vill_sd[`i'], "%6.2f")
		global mn`i' = strofreal(min[`i'], "%6.0f")
		global mx`i' = strofreal(max[`i'], "%6.0f")
	
	}
	
	
	/* Assign stars */
	do "${code}/pfm_spillovers/01_helpers/pfm_helper_stars.do"	
	
	
	
/* Make Table __________________________________________________________________*/

texdoc init "${spill_clean_tables}/pfm_spill_tables_results_hiv_priority_partner.tex", replace nolog

texdoc write 	{
texdoc write 	\def\sym#1{\ifmmode^{#1}\else\(^{#1}\)\fi}
texdoc write 	\begin{tabular}{l*{12}{c}}
texdoc write 	\Xhline{2\arrayrulewidth}\addlinespace[3pt]	

texdoc write    &\multicolumn{2}{c}{\textit{Voting}} &\multicolumn{8}{c}{\textit{Public Policy Priorities}} \tstrut \bstrut \\ \cmidrule(r){2-3} \cmidrule(r){4-11}

texdoc write    &\multicolumn{2}{c}{$var1} &\multicolumn{2}{c}{$var2} &\multicolumn{2}{c}{$var3}  &\multicolumn{2}{c}{$var4} &\multicolumn{2}{c}{$var5} \tstrut \bstrut \\ \cmidrule(r){2-3} \cmidrule(r){4-5} \cmidrule(r){6-7} \cmidrule(r){8-9} \cmidrule(r){10-11}

texdoc write    							& (1)				& (2)				& (3)			& (4)			& (5)			& (6)				& (7)			& (8)			& (9)			& (10)			\\

texdoc write 	HIV Treat      				& $b1\sym{$s1}  	& $b1c\sym{$s1c} 	& $b2\sym{$s2} 	& $b2c\sym{$s2c} & $b3\sym{$s3} & $b3c\sym{$s3c}	& $b4\sym{$s4}	& $b4c\sym{$s4c} & $b5\sym{$s5}	& $b5c\sym{$s5c} 	\\
texdoc write    Standard Error 				& $se1				& $se1c				& $se2    		& $se2c  		& $se3 			& $se3c				& $se4 			& $se4c			 & $se5 		& $se5c	\\
texdoc write 	\hline
texdoc write 	RI \$p\$-value 				& $p1				& $p1c 				& $p2			& $p2c			& $p3  			& $p3c				& $p4			& $p4c			& $p5			& $p5c				\\
texdoc write 	Hypothesis					& +					& + 				& +				& + 			& +				& +					& +				& +				& +				& +				\\
texdoc write 	Control Mean      			& $c1				& $c1				& $c2			& $c2			& $c3  			& $c3				& $c4			& $c4			& $c5			& $c5			\\
texdoc write 	Control SD 					& $sd1				& $sd1				& $sd2			& $sd2			& $sd3 			& $sd3				& $sd4			& $sd4			& $sd5			& $sd5		\\
texdoc write	DV Range					& [$mn1-$mx1]		& [$mn1-$mx1]		& [$mn2-$mx2]	& [$mn2-$mx2]	& [$mn3-$mx3] 	& [$mn3-$mx3]		& [$mn4-$mx4]	& [$mn4-$mx4]	& [$mn5-$mx5]	& [$mn5-$mx5] \\	
texdoc write 	Blocked FE 					& Yes 				& Yes 				& Yes   		& Yes  			& Yes			& Yes				& Yes			& Yes			& Yes			& Yes			\\
texdoc write 	Controls 					& $ctl1 			& $ctl1c 			& $ctl2   		& $ctl2c  		& $ctl3			& $ctl3c 			& $ctl4			& $ctl4c		& $ctl5			& $ctl5c	\\
texdoc write 	Adj-\$ R^2\$				& $r1 				& $r1c 				& $r2   		& $r2c 			& $r3			& $r3c				& $r4			& $r4c			& $r5			& $r5c			\\
texdoc write 	Observations      			& $n1 				& $n1c 				& $n2   		& $n2c 			& $n3			& $n3c				& $n4			& $n4c			& $n5			& $n5c			\\
texdoc write 	\Xhline{2\arrayrulewidth}
texdoc write 	\end{tabular}
texdoc write 	}

texdoc close









